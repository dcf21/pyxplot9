# EX_PENDULUM.PPL
#
# The code in this file is part of PyXPlot
# <http://www.pyxplot.org.uk>
#
# Copyright (C) 2006-2012 Dominic Ford <coders@pyxplot.org.uk>
#               2008-2012 Ross Church
#
# $Id$
#
# PyXPlot is free software; you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the Free Software
# Foundation; either version 2 of the License, or (at your option) any later
# version.
#
# You should have received a copy of the GNU General Public License along with
# PyXPlot; if not, write to the Free Software Foundation, Inc., 51 Franklin
# Street, Fifth Floor, Boston, MA  02110-1301, USA

# ----------------------------------------------------------------------------

# This script produces a figure for the PyXPlot Users' Guide

# Initialise
reset

# Set output destination
title = "ex_pendulum" ; load "examples/fig_init.ppl"

# Main body of figure script
theta_approx(a,t) = a * sin(2*pi*t)
theta_exact (a,t) = 2*asin(sin(a/2)*jacobi_sn(2*pi*t,sin(a/2)))

set unit of angle degrees
set key below
set xlabel 'Time / $\sqrt{g/l}$'
set ylabel '$\theta$'
omega = unit(30*deg)
plot [0:4] theta_approx(omega,x) title 'Approximate solution', \
           theta_exact (omega,x) title 'Exact solution'

# Set output destination
load "examples/fig_end.ppl"
title = "ex_pendulum2" ; load "examples/fig_init.ppl"

# Main body of figure script
subroutine PendulumDivergenceTime(omega, deviation)
 {
  for t=0 to 20 step 0.05
   {
    approx = theta_approx(omega,t)
    exact  = theta_exact (omega,t)
    if (abs(approx-exact)>deviation) { ;break; }
   }
  return t
 }

set key top right
set xlabel 'Amplitude of swing'
set ylabel 'Time / $\sqrt{g/l}$ taken to diverge'
set samples 40
plot [unit(5*deg):unit(30*deg)][0:19] \
  PendulumDivergenceTime(x,unit(20*deg)) title "$20^\circ$ deviation", \
  PendulumDivergenceTime(x,unit(10*deg)) title "$10^\circ$ deviation", \
  PendulumDivergenceTime(x,unit( 5*deg)) title "$ 5^\circ$ deviation"

# Call common cleanup script
load "examples/fig_end.ppl"

